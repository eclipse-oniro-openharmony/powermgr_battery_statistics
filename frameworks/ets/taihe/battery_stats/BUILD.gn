# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../batterystats.gni")

copy_taihe_idl("copy_batterystats") {
  sources = [ "idl/ohos.batteryStatistics.taihe" ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_batterystats" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.batteryStatistics.ani.cpp",
    "$taihe_generated_file_path/src/ohos.batteryStatistics.abi.c",
  ]
}

taihe_shared_library("batterystats_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "$taihe_generated_file_path"

  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "./include" ]
  configs = [
    "${batterystats_utils_path}:batterystats_utils_config",
    "${batterystats_utils_path}:coverage_flags",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.batteryStatistics.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${batterystats_inner_api}:batterystats_client",
    "${batterystats_utils_path}:batterystats_utils",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]
  subsystem_name = "powermgr"
  part_name = "$batterystats_part_name"
}

generate_static_abc("batterystats_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.batteryStatistics.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/batterystats_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("batterystats_etc") {
  source = "$target_out_dir/batterystats_abc.abc"
  module_install_dir = "framework"
  deps = [ ":batterystats_abc" ]
  subsystem_name = "powermgr"
  part_name = "$batterystats_part_name"
}
